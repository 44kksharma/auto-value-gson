apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group 'com.ryanharter.autogson'
version '1.0-SNAPSHOT'
def artifact = 'auto-gson'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.squareup:javapoet:1.1.0'
    compile 'com.google.auto.value:auto-value:1.2-SNAPSHOT'
    compile 'com.google.auto.service:auto-service:1.0-rc2'
    compile 'com.google.auto:auto-common:0.4'
    compile 'com.google.code.gson:gson:2.3.1'

    testCompile 'junit:junit:4.11'
    testCompile 'com.google.truth:truth:jar:0.27'
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
            : "https://oss.sonatype.org/content/repositories/snapshots/"
}

publishing {
    publications {
        processor(MavenPublication) {
            groupId project.group
            artifactId artifact
            version project.version

            from components.java
        }
    }
    repositories {
        maven {
            url isReleaseBuild() ? getReleaseRepositoryUrl() : getSnapshotRepositoryUrl()
            credentials() {
                username NEXUS_USERNAME
                password NEXUS_PASSWORD
            }
        }
    }
}
